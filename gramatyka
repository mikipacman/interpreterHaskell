-- Standardowe, trochę okrojone C
			
entrypoints Program ;
Program.    Program ::= [Decl] ;

VDecl.      Decl ::= BasicType [VarDeclItem] ";" ;
ADecl.	 	Decl ::= ArrType [VarDeclItem] ";" ;
FDecl.      Decl ::= BasicType Ident "(" [FuncDeclItem] ")" "{" [Decl] [Stmt] "return" Expr ";" "}" ;
separator   Decl "" ; 

NoInit.     VarDeclItem ::= Ident ; 
Init.       VarDeclItem ::= Ident "=" Expr ;
separator   nonempty VarDeclItem "," ;

FDItem.     FuncDeclItem ::= BasicType Ident ;
separator   FuncDeclItem "," ;

Block.      Block ::= "{" [Stmt] "}" ;

VarAs.      Assign ::= Ident [Acc] "=" Expr ;
separator   Assign "," ;

EmptyStmt.  Stmt ::= ";" ;
IfStmt.     Stmt ::= "if" "(" Expr ")" Block ;
IfElseStmt. Stmt ::= "if" "(" Expr ")" Block "else" Block ;
WhileStmt.  Stmt ::= "while" "(" Expr ")" Block ;
ForStmt.    Stmt ::= "for" "(" Decl [Expr] ";" [Assign] ")" Block ;
AsStmt.     Stmt ::= Assign ";" ;
ExprStmt.   Stmt ::= Expr ";" ;
SCostStmt.  Stmt ::= CountCost ";" ;
separator   Stmt "" ;

IntT.       BasicType ::= "int" ;
StringT.    BasicType ::= "string" ;
BoolT.      BasicType ::= "bool" ;

ArrT.		ArrType ::= BasicType [Acc] ;

IntL.       Literal ::= Integer ;
StringL.    Literal ::= String ;
TrueL.      Literal ::= "true" ; 
FalseL.     Literal ::= "false" ;

Acc. 		Acc ::= "[" Expr "]" ;
separator 	Acc "" ;

ExprLit.   	Expr6 ::= Literal ;
ExprGC.    	Expr6 ::= GetCost ;
ExprBr.    	Expr6 ::= "(" Expr ")" ;
ExprCall.  	Expr6 ::= Ident "(" [Expr] ")" ;
ExprAcc.   	Expr6 ::= Ident [Acc] ;
Neg.       	Expr5 ::= "-" Expr6 ;
Not.       	Expr5 ::= "!" Expr6 ;
EMul.      	Expr4 ::= Expr4 MulOp Expr5 ;
EAdd.      	Expr3 ::= Expr3 AddOp Expr4 ;
ERel.      	Expr2 ::= Expr2 RelOp Expr3 ;
EAnd.      	Expr1 ::= Expr2 "&&" Expr1 ;
EOr.       	Expr ::= Expr1 "||" Expr ;
coercions  	Expr 6 ;
separator  	Expr "," ;

AOp.       	Op ::= AddOp ;
MOp.       	Op ::= MulOp ;
ROp.       	Op ::= RelOp ;

Plus.      	AddOp ::= "+" ;
Minus.     	AddOp ::= "-" ;
Mul.       	MulOp ::= "*" ;
Div.       	MulOp ::= "/" ;
Mod.       	MulOp ::= "%" ;
Less.      	RelOp ::= "<" ;
LessEq.    	RelOp ::= "<=" ;
Great.     	RelOp ::= ">" ;
GreatEq.   	RelOp ::= ">=" ;
Eq.        	RelOp ::= "==" ;
NotEq.     	RelOp ::= "!=" ;	


-- Nowości

CRead.     	CountCost ::= "count" Ident "read_cost" Expr "with" Ident ;
CWrite.    	CountCost ::= "count" Ident "write_cost" Expr "with" Ident ;
COp.       	CountCost ::= "count" Op "cost" Expr "with" Ident ;	

GRead.     	GetCost ::= "get" "read_cost" Ident ;
GWrite.    	GetCost ::= "get" "write_cost" Ident ;
GOpCost.   	GetCost ::= "get" "cost" Op ;


-- Komentarze

comment    	"//" ;
comment    	"/*" "*/" ;
